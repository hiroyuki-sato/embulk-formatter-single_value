plugins {
    id "com.jfrog.bintray" version "1.1"
    id "java"
    id "checkstyle"
    id "maven-publish"
    id "org.embulk.embulk-plugins" version "0.4.2"
}
repositories {
    mavenCentral()
    jcenter()
}

embulkPlugin {
    mainClass = "org.embulk.formatter.single_value.SingleValueFormatterPlugin"
    category = "formatter"
    type = "single_value"
}

group = "io.github.sonots"
version = "0.3.1"
description = "Embulk formatter plugin to output values of a single column."

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compileOnly "org.embulk:embulk-api:0.10.31"
    compileOnly "org.embulk:embulk-spi:0.10.31"
    compile("org.embulk:embulk-util-config:0.2.1") {
        // They conflict with embulk-core. They are once excluded here,
        // and added explicitly with versions exactly the same with embulk-core:0.10.19.
        exclude group: "com.fasterxml.jackson.core", module: "jackson-annotations"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-core"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-databind"
        exclude group: "com.fasterxml.jackson.datatype", module: "jackson-datatype-jdk8"
        exclude group: "javax.validation", module: "validation-api"
    }
    compile "org.embulk:embulk-util-timestamp:0.2.1"
    compile "org.embulk:embulk-util-text:0.1.1"

    // compile "YOUR_JAR_DEPENDENCY_GROUP:YOUR_JAR_DEPENDENCY_MODULE:YOUR_JAR_DEPENDENCY_VERSION"
    testCompile "org.embulk:embulk-api:0.10.31"
    testCompile "org.embulk:embulk-spi:0.10.31"
    testCompile "org.embulk:embulk-core:0.10.31"
    testCompile "org.embulk:embulk-deps:0.10.31"
    testCompile "junit:junit:4.+"
}

checkstyle {
    configFile = file("${project.rootDir}/config/checkstyle/checkstyle.xml")
    toolVersion = '6.14.1'
}
checkstyleMain {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
checkstyleTest {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
task checkstyle(type: Checkstyle) {
    classpath = sourceSets.main.output + sourceSets.test.output
    source = sourceSets.main.allJava + sourceSets.test.allJava
}

publishing {
    publications {
        embulkPluginMaven(MavenPublication) {  // Publish it with "publishEmbulkPluginMavenPublicationToMavenRepository".
            from components.java  // Must be "components.java". The dependency modification works only for it.
        }
    }
    repositories {
        maven {
            url = "${project.buildDir}/mavenPublishLocal"
        }
    }
}

gem {
    from("LICENSE.txt")  // Optional -- if you need other files in the gem.
    authors = [ "Naotoshi Seo" ]
    email = [ "sonots@gmail.com" ]
    // "description" of the gem is copied from "description" of your Gradle project.
    summary = "Embulk formatter plugin to output values of a single column"
    homepage = "https://github.com/sonots/embulk-formatter-single_value"
    licenses = [ "MIT" ]
    //metadata = []
}
gemPush {
    host = "https://rubygems.org"
}

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
}
